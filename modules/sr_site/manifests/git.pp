# NB: a lot of configuration here is related to the old lumps of code SR used
# to generate RSS feeds for pushes/commits, which were replaced by git, and
# are thus commented out.

class sr_site::git($git_root) {
  # Git package is installed in the kickstart file,

  # A user for git tasks to be run as.
  user { 'git':
    ensure => present,
    comment => 'Owner of git maintenence scripts and cron jobs',
    shell => '/sbin/nologin',
    gid => 'users', # Dummy group, I've no idea what it should be in.
    home => '/'
  }

  if !$devmode {
    # On a production machine, the dir /srv/git/scripts will already exist
    # as a matter of course (because git is stored on said production machine)
    cron { 'cgit-reconf':
      command => '/srv/git/scripts/cgit-reconf',
      hour => '*/4',
      minute => '13',
      user => 'git',
      require => Package['GitPython'],
    }
  } else {
    # On a dev machine, the scripts repo needs to be checked out manually.
    cron { 'cgit-reconf':
      command => '/srv/git/scripts/cgit-reconf',
      hour => '*/4',
      minute => '13',
      user => 'git',
      require => [Vcsrepo['/srv/git/scripts'], Package['GitPython']],
    }
  }

  # Ensure the git serving dir exists
  $git_dir = '/srv/git'
  file { $git_dir:
    ensure => directory,
    owner => 'git',
    group => 'users',
    mode => '0755',
    require => User['git'],
  }

  # Our custom cgit logo
  file { "${git_dir}/srgit.png":
    ensure => 'file',
    owner => 'git',
    group => 'users',
    mode => '0644',
    source => 'puppet:///modules/sr_site/srgit.png',
    require => File[$git_dir],
  }


  # Maintain a clone of the git admin scripts.
  if $devmode {
    vcsrepo { '/srv/git/scripts':
      ensure => present,
      provider => git,
      source => "${git_root}/scripts",
      revision => 'origin/master',
      owner => 'git',
      group => 'users',
      require => [File['/srv/git'], Exec['ldap-groups-flushed']],
    }
  }

  package { 'GitPython':
    ensure => present,
  }

  # The cgit package is a decent repo explorer / gui, see srobo.org/cgit
  package { 'cgit':
    ensure => present,
  }

  # Config file for serving our repos; no contents provided, because they're
  # auto generated by the cgit-reconf cron job.
  file { '/etc/cgitrc':
    ensure => present,
    owner => 'git',
    group => 'root',
    mode => '0644',
    require => [Package['cgit'], Exec['ldap-groups-flushed']],
  }
}
